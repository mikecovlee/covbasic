darwin.load()
define expr=runtime.build(darwin.input_box("Sketchpad","Please enter a expression:","",false))
define x_offset=0
define y_offset=0
define zoom=5
define dzoom=1
define center_x=0
define center_y=0
constant pix=darwin.pixel('+',true,false,darwin.blue,darwin.black)
function f(x)
  return runtime.solve(expr)
end
loop
  darwin.fit_drawable()
  darwin.clear_drawable()
  if(darwin.is_kb_hit())
    switch darwin.get_kb_hit()
    	case 'w'
			++y_offset
    	end
    	case 's'
    	  --y_offset
    	end
    	case 'a'
    	  ++x_offset
    	end
    	case 'd'
    	  --x_offset
    	end
    	case 'q'
    	  zoom=zoom+dzoom
    	end
    	case 'e'
    		if(zoom>2)
    			zoom=zoom-dzoom
    		end
    	end
    end
  end
  center_x=0.5*darwin.get_width()
  center_y=0.5*darwin.get_height()
  for i=0 to darwin.get_width()-2
    darwin.draw_line(i,center_y-f((i-center_x-x_offset)/(2*zoom))*zoom+y_offset,i+1,center_y-f((i+1-center_x-x_offset)/(2*zoom))*zoom+y_offset,pix)
  end
  darwin.draw_line(0,center_y+y_offset,darwin.get_width()-1,center_y+y_offset,darwin.pixel('-',true,false,darwin.white,darwin.black))
  darwin.draw_line(center_x+x_offset,0,center_x+x_offset,darwin.get_height()-1,darwin.pixel('|',true,false,darwin.white,darwin.black))
  darwin.draw_point(center_x+x_offset,center_y+y_offset,darwin.pixel('+',true,false,darwin.white,darwin.black))
  darwin.draw_point(center_x+x_offset,0,darwin.pixel('^',true,false,darwin.white,darwin.black))
  darwin.draw_point(darwin.get_width()-1,center_y+y_offset,darwin.pixel('>',true,false,darwin.white,darwin.black))
  define i=-to_integer((center_x+x_offset)/(zoom*2))
  while(center_x+x_offset+2*i*zoom<darwin.get_width()-1)
  	if(i!=0)
  		darwin.draw_point(center_x+x_offset+2*i*zoom,center_y+y_offset,darwin.pixel('|',true,false,darwin.white,darwin.black))
  		define str=to_string(i)
  		darwin.draw_string(center_x+x_offset+2*i*zoom-0.5*str.size(),center_y+y_offset+1,str,darwin.pixel(' ',true,false,darwin.white,darwin.black))
	end
	++i
  end
  define i=to_integer((center_y-y_offset)/zoom)
  while(center_y+y_offset+i*zoom>0)
	if(i!=0)
  		darwin.draw_point(center_x+x_offset,center_y+y_offset+i*zoom,darwin.pixel('-',true,false,darwin.white,darwin.black))
  		define str=to_string(-i)
  		darwin.draw_string(center_x+x_offset-str.size(),center_y+y_offset+i*zoom,str,darwin.pixel(' ',true,false,darwin.white,darwin.black))
	end
	--i
  end
  darwin.draw_string(0,0,"Sketchpad",darwin.pixel(' ',true,false,darwin.black,darwin.white))
  darwin.update_drawable()
end
